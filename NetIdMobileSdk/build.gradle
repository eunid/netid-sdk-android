plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlinx-serialization'
    id 'kotlin-parcelize'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace = 'de.netid.mobile.sdk'
    compileSdk 32

    defaultConfig {
        minSdk 23
        targetSdk 32
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        manifestPlaceholders = [appAuthRedirectScheme: 'de.netid.mobile.sdk.netidmobilesdk']
        aarMetadata {
            minCompileSdk = 23
        }
        versionName = "1.0.0"
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
        kotlinOptions {
        jvmTarget = '1.8'
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId 'de.netid.mobile'
            artifactId 'netidmobilesdk'
            version = android.defaultConfig.versionName

            artifact("$buildDir/outputs/aar/sdk-release.aar")

            pom {
                name = "netID SDK for Android"
                description = "The netID MobileSDK facilitates the use of the netID federated login and privacy management services."
                url = 'https://github.com/eunid/netid-sdk-android/'

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                scm {
                    url = 'https://github.com/eunid/netid-sdk-android/'
                    connection = 'scm:git:git://github.com:eunid/netid-sdk-android.git'
                    developerConnection = 'scm:git:ssh://github.com:eunid/netid-sdk-android.git'
                }

                developers {
                    developer {
                        id = 'eunid'
                        name = 'European netID Foundation'
                        url = 'https://github.com/eunid'
                    }
                }

                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username sonatype_user
                password sonatype_pwd
            }
        }
    }
}

signing {
    sign publishing.publications
}

dependencies {
    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'net.openid:appauth:0.11.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.4.0-RC'
    implementation 'com.squareup.okhttp3:okhttp:4.10.0'
}
