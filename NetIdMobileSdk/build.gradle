import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'kotlinx-serialization'
    id 'kotlin-parcelize'
    id 'maven-publish'
    id 'signing'
}

android {
    namespace = 'de.netid.mobile.sdk'
    compileSdk 35

    defaultConfig {
        minSdk 23
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
        manifestPlaceholders = [appAuthRedirectScheme: 'de.netid.mobile.sdk.netidmobilesdk']
        aarMetadata {
            minCompileSdk = 23
        }
    }

    buildFeatures {
        viewBinding true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlin {
        compilerOptions {
            jvmTarget = JvmTarget.JVM_17
        }
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId 'de.netid.mobile'
            artifactId 'netidmobilesdk'
            version = android.defaultConfig.versionName

            artifact("$buildDir/outputs/aar/sdk-release.aar")

            pom {
                name = "netID SDK for Android"
                description = "The netID MobileSDK facilitates the use of the netID federated login and privacy management services."
                url = 'https://github.com/eunid/netid-sdk-android/'
                packaging 'aar'

                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution = 'repo'
                    }
                }

                scm {
                    url = 'https://github.com/eunid/netid-sdk-android/'
                    connection = 'scm:git:git://github.com:eunid/netid-sdk-android.git'
                    developerConnection = 'scm:git:ssh://github.com:eunid/netid-sdk-android.git'
                }

                developers {
                    developer {
                        id = 'eunid'
                        name = 'European netID Foundation'
                        url = 'https://github.com/eunid'
                    }
                }

                withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')

                    project.configurations.implementation.allDependencies.each {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = "sonatype"
            url = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
            credentials {
                username 'username'
                password 'password'
            }
        }
    }
}

signing {
    sign publishing.publications
}

dependencies {
    implementation 'androidx.core:core-ktx:1.16.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'net.openid:appauth:0.11.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-serialization-json:1.9.0'
    implementation 'com.squareup.okhttp3:okhttp:5.1.0'
}
